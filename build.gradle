plugins {
    id 'java'
    id 'maven-publish'
}

def simpleBeansGroupId = 'ru.leonidm'
def simpleBeansArtifactId = 'SimpleBeans'
def simpleBeansVersion = '1.0.1'

group simpleBeansGroupId
version simpleBeansVersion

repositories {
    mavenCentral()

    maven { url 'https://mvn.smashup.ru/releases' }
}

sourceCompatibility = 17
targetCompatibility = 17
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
tasks.withType(Javadoc).configureEach { options.encoding = 'UTF-8' }

dependencies {
    implementation 'org.jetbrains:annotations:23.1.0'

    implementation 'ru.leonidm:CommonsM:1.0.1'

    implementation 'org.apache.bcel:bcel:6.7.0'
    implementation 'net.bytebuddy:byte-buddy:1.14.2'

    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.slf4j:slf4j-api:2.0.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

jar {
    manifest {
        attributes['Main-Class'] = "ru.leonidm.simplebeans.SimpleBeans"
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

tasks.register('sourceJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

tasks.register('packageJavadoc', Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = simpleBeansGroupId
            artifactId = simpleBeansArtifactId
            version = simpleBeansVersion

            from components.java
            artifact sourceJar
            artifact packageJavadoc
        }
    }
    repositories {
        maven {
            name = "smashUpRepository"
            url "https://mvn.smashup.ru/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

test {
    useJUnitPlatform()
}